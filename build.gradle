buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'


ext.buildnumber = 0
if (System.getenv('BUILD_NUMBER') != null)
    project.buildnumber = System.getenv('BUILD_NUMBER')
else
    logger.lifecycle "No build number detected, setting to 0"

group= "com.theprogrammingturkey.customUI"
archivesBaseName= "CustomUI"
version= "${mc_version}-${mod_version}.${buildnumber}"

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = "run-1.11"

    mappings = "snapshot_20161111"
    
    replace "@VERSION@", project.version
    replaceIn "CustomUICore.java"
    replaceIn "mcmod.info"
}

repositories {
	mavenCentral()
	
    maven { // The repo from which to get GobbleCore
        name "GobbleCore Repo"
        url "http://maven.theprogrammingturkey.com/"
    }
}

dependencies {
	compile "com.theprogrammingturkey.gobblecore:1.11:1.11.2-0.1.3.16"
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

build.dependsOn sourceJar
 
 publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            from components.java
 
            artifact sourceJar
        }
    }

    repositories {
        if (project.hasProperty('mavendir')) {
            maven { url mavendir }
        }
        else{
        	logger.error('Failed to find maven directory')
        }
    }
}
